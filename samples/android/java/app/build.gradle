plugins {
    id 'com.android.application'
}

//JniGen settings
def jniGenProps = new Object() {
    def generatedPath = "src/main/cpp/generated"
    def outputListName = "outputs.txt"
    def additionalClasses = ["java.lang.Byte"]
}

dependencies {

    //JNI annotations
    compileOnly("smjni.jnigen:annotations:3.2")
    //JNI code generator
    annotationProcessor("smjni.jnigen:processor:3.2")

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}


android {
    compileSdk 30

    defaultConfig {
        applicationId "com.example.myapplication"
        minSdk 21
        targetSdk 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        //Pass options for JniGen
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                        "smjni.jnigen.dest.path"       : file(jniGenProps.generatedPath).path,
                        "smjni.jnigen.own.dest.path"   : "true",
                        "smjni.jnigen.output.list.name": jniGenProps.outputListName,
                        "smjni.jnigen.expose.extra"    : jniGenProps.additionalClasses.join(";").toString()
                ]
            }
        }
        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.10.2'
        }
    }
}


//This makes Gradle rebuild Java compilation (and so run annotation processor)
//when any of the generated files are missing
//Use libraryVariants if you are building a library
android.applicationVariants.all { variant ->

    variant.javaCompileProvider.get().outputs.upToDateWhen {

        def jniGenOutputList = file("${jniGenProps.generatedPath}/${jniGenProps.outputListName}")

        if (!jniGenOutputList.exists()) {
            return false
        }

        for(line in jniGenOutputList) {
            if (!file("${jniGenProps.generatedPath}/$line").exists()) {
                return false
            }
        }
        return true
    }
}

//Clean generated headers on project clean
task cleanJNIHeaders(type: Delete) {
    delete file("${jniGenProps.generatedPath}")
}
clean.dependsOn cleanJNIHeaders


//Make Java compilation (and JniGen code generation) run before CMake build
tasks.whenTaskAdded { theTask ->
    def match = theTask.name =~ ~/^buildCMake(.*)$/
    if (match) {
        theTask.dependsOn "compile${match.group(1)}JavaWithJavac"
    }
}
