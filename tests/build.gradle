/*
 Copyright 2019 SmJNI Contributors

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
*/

import java.util.regex.Pattern

buildscript {
    repositories {
        mavenCentral()
    }
}

allprojects {
    repositories {
        mavenLocal()
        mavenCentral()
    }
}

apply plugin: 'java'

String JNIGEN_GENERATED_PATH = "src/cpp/generated"
String JNIGEN_OUTPUT_LIST_NAME = "outputs.txt"


java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    implementation 'smjni.jnigen:annotations:SNAPSHOT'
    implementation 'org.junit.jupiter:junit-jupiter-api:5.8.0'
    annotationProcessor("smjni.jnigen:processor:SNAPSHOT@jar") {
        transitive true
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
    options.compilerArgs = [
                "-Asmjni.jnigen.dest.path=" + file(JNIGEN_GENERATED_PATH).path,
                "-Asmjni.jnigen.output.list.name=" + JNIGEN_OUTPUT_LIST_NAME,
                "-Asmjni.jnigen.expose.extra=" + ["java.lang.AssertionError"].join(";").toString()
        ]
    outputs.file("$JNIGEN_GENERATED_PATH/$JNIGEN_OUTPUT_LIST_NAME")
}

sourceSets {
    main.java.srcDirs = ['src/java']
}

task cleanStaleJNIHeaders(dependsOn: compileTestJava) {

    inputs.files file("$JNIGEN_GENERATED_PATH/$JNIGEN_OUTPUT_LIST_NAME")

    doLast {
        def generated = new HashSet<String>()
        def outputs = file("$JNIGEN_GENERATED_PATH/$JNIGEN_OUTPUT_LIST_NAME")
        if (outputs.exists()) {
            outputs.eachLine { line ->
                generated.add(line)
            }
        }
        def existing = project.fileTree(dir: file("$JNIGEN_GENERATED_PATH"), include: "*.h").files
        def toDelete = []
        for (file in existing) {
            if (!generated.contains(file.name))
                toDelete.add(file)
        }

        project.delete { delete toDelete }
    }
}

task makeJNIHeaders(dependsOn: cleanStaleJNIHeaders) {

}

task copyRuntimeLibs(type: Copy) {
    into(project.buildDir.absolutePath + "/libs")
    from(configurations.runtimeClasspath)
}

jar {
    dependsOn("copyRuntimeLibs")
    manifest {
        attributes 'Implementation-Title': 'SmJNI Tests',
                   'Implementation-Version': project.version,
                   'Main-Class': 'smjni.tests.TestSmJNI',
                   'Class-Path': configurations.runtimeClasspath.collect { "libs/"+ it.getName() }.join(' ')
    }
    
    setArchiveFileName("smjnitests.jar")
    setDestinationDirectory(rootProject.buildDir)
}