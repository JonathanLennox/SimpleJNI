/*
 Copyright 2021 SimpleJNI Contributors

 Incorporates work covered by the following copyright
 Copyright 2014 Smartsheet Inc.
 Copyright 2019 SmJNI Contributors

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
*/
publishing {
    repositories {
        if (project.hasProperty("localRepo")) {
            maven {
                name "localRepo"
                url project.getProperty("localRepo")
            }
        }
        if (project.hasProperty("ossrhUsername") && project.hasProperty("ossrhPassword")) {
            maven {
                name "ossrh"
                url project.version.endsWith('SNAPSHOT') ?
                        "https://s01.oss.sonatype.org/content/repositories/snapshots/" :
                        "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
                credentials {
                    username = project.getProperty('ossrhUsername')
                    password = project.getProperty('ossrhPassword')
                }
            }
        }
    }
    publications {
        thePublication(MavenPublication) {
            artifactId "${artifactIdPrefix}${project.name}"
            from components.java
            artifact sourceJar
            artifact javadocJar
            pom {
                name = libraryPomName
                description = libraryDescription
                url = websiteUrl
                licenses {
                    license {
                        name = licenseName
                        url = licenseUrl
                    }
                }
                scm {
                    connection = gitUrl
                    developerConnection = gitUrl
                    url = vcsUrl
                }
                developers {
                    developer {
                        id = 'gershnik'
                        name = 'gershnik'
                        email = 'gershnik-maven@gershnik.info'
                    }
                }
            }
        }
    }
}

if (!project.version.endsWith('SNAPSHOT')) {
    signing {
        sign publishing.publications.thePublication
    }
}
