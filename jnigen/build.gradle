/*
 Copyright 2021 SimpleJNI Contributors

 Incorporates work covered by the following copyright
 Copyright 2014 Smartsheet Inc.
 Copyright 2019 SmJNI Contributors

 Licensed under the Apache License, Version 2.0 (the "License");
 you may not use this file except in compliance with the License.
 You may obtain a copy of the License at

     http://www.apache.org/licenses/LICENSE-2.0

 Unless required by applicable law or agreed to in writing, software
 distributed under the License is distributed on an "AS IS" BASIS,
 WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 See the License for the specific language governing permissions and
 limitations under the License.
*/
buildscript {
    ext.kotlin_version = '1.5.31'
    ext.junit_version = '5.8.1'

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "$kotlin_version" apply false
}

    def getOurVersion() {

    if (project.hasProperty("code.version")) {

        return project.getProperty("code.version")

    } else if (file('../.git').isDirectory()) {

        def tagStdOut = new ByteArrayOutputStream()
        exec {
            commandLine "git", "describe", "--tags", "--abbrev=0"
            standardOutput = tagStdOut
        }
        return tagStdOut.toString().trim()

    }

    throw new RuntimeException("version cannot be obtained from Git or properties")
}



allprojects {
    repositories {
        mavenCentral()
    }

    project.version = getOurVersion()
    project.group "io.github.gershnik"
    project.ext.artifactIdPrefix = "smjni-jnigen-"
    project.ext.gitUrl = "git@github.com:gershnik/SimpleJNI.git"
    project.ext.licenseName = "The Apache License, Version 2.0"
    project.ext.licenseUrl = "http://www.apache.org/licenses/LICENSE-2.0.txt"
    project.ext.websiteUrl = 'https://github.com/gershnik/SimpleJNI'
    project.ext.vcsUrl = 'https://github.com/gershnik/SimpleJNI.git'
    project.ext.issueTrackerUrl = 'https://github.com/gershnik/SimpleJNI/issues'

    project.ext.kotlinJvmTarget = "1.8"
}

task bundleCpp(type: Zip, group: 'publishing') {
    from('../src') {
        include '**/*.cpp'
        include '**/*.h'
        into 'src'
    }
    from('../inc') {
        include '**/*.h'
        into 'inc'
    }
    from('../CMakeLists.txt',
         '../COPYRIGHT.txt',
         '../LICENSE.txt',
         '../NOTICE.txt',
         '../README.md')

    setArchiveFileName("SimpleJNI-${project.version}-cpp-only.zip")
    setDestinationDirectory(rootProject.buildDir)
}

task bundleJava(type: Zip, group: 'publishing',
                dependsOn: ['annotations:assemble','processor:assemble','kprocessor:assemble']) {

    from(new File(rootProject.buildDir, 'jnigen.jar'),
         new File(rootProject.buildDir, 'kjnigen.jar'),
         new File(rootProject.buildDir, 'jnigen-annotations.jar'))

    includeEmptyDirs true
    setArchiveFileName("SimpleJNI-${project.version}-jnigen.zip")
    setDestinationDirectory(rootProject.buildDir)
}


